# Maintainer: ahjolinna <ahjolinna@yahoo.com>

pkgname=freshplayerplugin-git
pkgver=v0.3.4.r21.g7ba5f2d
pkgrel=1
pkgdesc='PPAPI-host NPAPI-plugin adapter.'
arch=('x86_64')
url='https://github.com/i-rinat/freshplayerplugin'
license=( 'MIT' )
 depends=('pepperflashplugin' 'pango' 'alsa-lib' 'freetype2' 'cairo' 'glib2' 'libpulse' 
          'libx11' 'libevent' 'gtk3' 'libgl' 'openssl' 'ffmpeg' 'libxrandr'
          'libxrender' 'libxcursor' 'v4l-utils' 'libva' 'jack2')
makedepends=( 'cmake' 'ragel' 'git' )
conflicts=( 'freshplayerplugin' 'flashplugin' 'chromium-pepper-flash')
source=( "${pkgname}::git+${url}" "${pkgname}.install" "freshwrapper.conf")
backup=('etc/freshwrapper.conf')
install="${pkgname}.install"


pkgver() {
	cd "$pkgname"

	if git_version=$( git describe --long --tags 2>/dev/null ); then
		IFS='-' read last_tag tag_rev commit <<< "$git_version"
		printf '%s.r%s.%s' "$last_tag" "$tag_rev" "$commit"
	else
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	fi
}
 
build() {
   cd "$pkgname"
   cmake -DTRACE_ALL=1 -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DWITH_GTK=3
   #cmake -DTRACE_ALL=1 -DCMAKE_BUILD_TYPE=Release -DWITH_HWDEC=1 -DCMAKE_INSTALL_PREFIX=/usr -DWITH_GTK=3
  make
}
 
package() {
   cd "$pkgname"
  install -d "$pkgdir/usr/lib/mozilla/plugins"
  install -m644 libfreshwrapper-*.so "$pkgdir/usr/lib/mozilla/plugins"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 $srcdir/freshwrapper.conf "$pkgdir/etc/freshwrapper.conf"
      # configure path to libpepflashplayer.so
      sed -r -e 's|^(pepperflash_path).*|\1 = "/usr/lib/PepperFlash/libpepflashplayer.so"|' \
             -i "$pkgdir/etc/freshwrapper.conf"
}
sha1sums=('SKIP'
          '331a3b3877249eaf1c3db917bde1dea6c4d374ab'
          '9ddec08f4e328fd0e2f42268080ca6c965454c65')

