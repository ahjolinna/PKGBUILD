# Maintainer: ahjolinna <ahjolinna@yahoo.com>

pkgname=libcurl-compat
_pkgname=curl
pkgver=7.46.0
pkgrel=1
pkgdesc="An URL retrieval library (old version)"
arch=('x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'gnutls' 'openssl' 'zlib')
options=('strip')
conflicts=('libcurl-gnutls')
source=("http://curl.haxx.se/download/${_pkgname}-$pkgver.tar.gz")
md5sums=('230e682d59bf8ab6eca36da1d39ebd75')
install=libcurl-compat.install

build() {
  config="./configure \
      --prefix=/usr \
      --disable-ldap \
      --disable-ldaps \
      --enable-ipv6 \
      --disable-manual \
      --disable-versioned-symbols \
      --enable-threaded-resolver \
      --without-gssapi \
      --without-libidn \
      --without-libssh2 \
      --with-random=/dev/urandom \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt"

  cp -a ${_pkgname}-$pkgver{,-gnutls}

  cd "${_pkgname}-$pkgver-gnutls"
  $config --without-ssl --with-gnutls
  make -C lib

  cd "../${_pkgname}-$pkgver"
  $config
  make -C lib
}

package() {
  cd "${_pkgname}-$pkgver-gnutls"
  make -C lib DESTDIR="$pkgdir" install
  mv $pkgdir/usr/lib/libcurl.so.4.4.0 \
    $pkgdir/usr/lib/libcurl-gnutls-compat.so.4.4.0
  cd "../${_pkgname}-$pkgver"
  make -C lib DESTDIR="$pkgdir" install
  mv $pkgdir/usr/lib/libcurl.so.4.4.0 \
    $pkgdir/usr/lib/libcurl-compat.so.4.4.0
  rm $pkgdir/usr/lib/libcurl.so{,.4}
  rm -rf $pkgdir/etc
  ln -s libcurl-compat.so.4.4.0  $pkgdir/usr/lib/libcurl.so.3
  ln -s libcurl-gnutls-compat.so.4.4.0  $pkgdir/usr/lib/libcurl-gnutls.so.3

  # license
  install -d "$pkgdir/usr/share/licenses"
  ln -s "$_pkgname" "$pkgdir/usr/share/licenses/$pkgname"
}
