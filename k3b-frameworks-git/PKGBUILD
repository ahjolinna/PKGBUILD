# Maintainer: ahjolinna <ahjolinna@yahoo.com>

pkgname=k3b-frameworks-git
pkgver=2.9.90.r5955.e8961fe
pkgrel=1
pkgdesc="Feature-rich and easy to handle CD burning application. KF5 Frameworks branch. (Git version)"
arch=('i686' 'x86_64')
url='http://k3b.sourceforge.net'
license=('GPL')
depends=('karchive' 'kconfig' 'ki18n' 'kcmutils' 'kcoreaddons' 'knotifyconfig' 'solid'
         'kfilemetadata' 'kiconthemes' 'kjobwidgets' 'kio' 'knotifications' 'kservice' 'kwidgetsaddons'
         'kxmlgui' 'libkcddb-frameworks-git' 'libsamplerate' 'libmad' 'ffmpeg' 'taglib' 'libmpcdec' 
         'libdvdread' 'cdrdao' 'cdrtools' 'shared-mime-info' 'libxft' 'qt5-webkit')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'pkg-config' 'dvd+rw-tools')
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager:    for vcd burning support'
            'transcode:    for advanced mpeg conversion support'
            'emovix:       for bootable multimedia cd/dvd support'
            'cdparanoia: for cd ripping support')
provides=('k3b')
conflicts=('k3b')
source=('git://anongit.kde.org/k3b.git#branch=kf5')
sha1sums=('SKIP')
install=k3b-frameworks-git.install

pkgver() {
  cd k3b
  _ver="$(cat CMakeLists.txt | grep -m3 -e 'K3B_VERSION_MAJOR' -e 'K3B_VERSION_MINOR' -e 'K3B_VERSION_RELEASE' | sed 's|K3B|KEB|' | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../k3b \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
