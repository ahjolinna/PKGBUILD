# Maintainer: ahjolinna <ahjolinna@yahoo.com>

pkgname=libmtp-git
_gitname=libmtp-code
pkgver=1.1.11.r1.ga67c9bc
pkgrel=1
pkgdesc="Library implementation of the Media Transfer Protocol"
arch=("x86_64")
url="http://libmtp.sourceforge.net"
license=('LGPL')
depends=('libusb' 'libgcrypt')
makedepends=('git')
conflicts=('libmtp')
provides=('libmtp')
source=("$_gitname"::'git://git.code.sf.net/p/libmtp/code')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long --tags | sed -r 's/libmtp-//' | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
  yes | ./autogen.sh
  ./configure --prefix=/usr --with-udev=/usr/lib/udev
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="${pkgdir}" install

  # fix broken udev rule
  sed -i "/^Unable to open/d" ${pkgdir}/usr/lib/udev/rules.d/69-libmtp.rules
}
