# Maintainer: ahjolinna <ahjolinna@yahoo.com>

# WARNING: The configure script will automatically enable any optional
# features it finds support for on your system. If you want to avoid
# linking against something you have installed, you'll have to disable
# it in the configure below. The package() script will attempt to
# update the dependencies based on dynamic libraries when packaging.

pkgname=mpv-git
_gitname=mpv
pkgver=42665.ga99b63d
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2 (git version)'
arch=('x86_64')
license=('GPL')
url='http://mpv.io'
screenshot='https://i.warosu.org/data/g/img/0475/21/1429034725169.png'
_undetected_depends=('desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
depends=('ffmpeg' "${_undetected_depends[@]}")
optdepends=('youtube-dl: for --ytdl'
            'yle-dl: for watching yle areena videos'
            'libva-intel-driver: hardware acceleration support for Intel GPU'
            'libvdpau: hardware acceleration support for AMD/NVIDIA gpu'
            'vapoursynth-plugin-mvtools: Realtime motion interpolating playback in mpv')
makedepends=('git' 'python3-docutils' 'x264' 'x265' 'wayland' 'samba' 'vapoursynth')
provides=('mpv')
conflicts=('mpv' 'mpv-build-git' 'mpv-ahjolinna-buid-git' 'mpv-vapoursynth' 'x265-hg')
options=('!emptydirs')
install=mpv.install
source=('git+https://github.com/mpv-player/mpv'
        'find-deps.py')
md5sums=('SKIP'
         'ffb774b13decbefc62908dda0332046b')
sha256sums=('SKIP'
            'ce974e160347202e0dc63f6a7a5a89e52d2cc1db2d000c661fddb9dc1d007c02')

pkgver() {
  cd "$srcdir/$_gitname"
  #_curtag="$(git rev-list --tags --max-count=1)"
  #_tagver="$(git describe --tags $_curtag | sed -e 's:^v::' -e 's:-:_:g')"
  #_commits="$(git rev-list --count HEAD --since=$_tagver)"
  #_sha="$(git rev-parse --short HEAD)"
  #printf "%s_%s_g%s" $_tagver $_commits $_sha
  echo "$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
   # point python to python3
sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python|' ${srcdir}/${_gitname}/bootstrap.py
# initialize
  cd "$srcdir/$_gitname"
  ./bootstrap.py
  sed -i 's/vendor-completions/site-functions/' wscript_build.py
 
 # waf looking for python2...
sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python|' ${srcdir}/${_gitname}/waf
}

build() {
  cd "$srcdir/$_gitname"

  CFLAGS="$CFLAGS -I/usr/include/samba-4.0"

  ./waf configure --prefix=/usr \
        --confdir=/etc/mpv \
        --enable-libmpv-shared \
        --enable-cdda \
        --enable-zsh-comp \
        --enable-alsa \
        --enable-pulse \
        --enable-openal \
        --enable-wayland \
        --enable-gl-wayland \
        --enable-egl-x11 \
        --enable-libsmbclient \
        --enable-dvdread \
        --enable-libv4l2 \
        --enable-gpl3 \
        --enable-vapoursynth \

  ./waf build
}

package() {
  cd "$srcdir/$_gitname"
  ./waf install --destdir="$pkgdir"

  # Update dependencies automatically based on dynamic libraries
  _detected_depends=($("$srcdir"/find-deps.py "$pkgdir"/usr/{bin/mpv,lib/libmpv.so}))
  echo 'Auto-detected dependencies:'
  echo "${_detected_depends[@]}" | fold -s -w 79 | sed 's/^/ /'
  depends=("${_detected_depends[@]}" "${_undetected_depends[@]}")
}
