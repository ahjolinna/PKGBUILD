# Maintainer: ahjolinna <ahjolinna@yahoo.com>
pkgname=okular-frameworks-git
pkgver=r7236.a704fbc
pkgrel=1
pkgdesc='Universal document viewer'
arch=(x86_64)
url='http://kde.org/applications/graphics/okular/'
license=(GPL)
depends=('kactivities' 'karchive' 'kbookmarks' 'kconfig' 'kconfigwidgets' 'kcoreaddons' 'kdbusaddons'
         'kjs' 'kdelibs4support' 'kio' 'khtml' 'kparts' 'kpty' 'threadweaver' 'kwallet' 'phonon-qt5' 'libkscreen'
         'libxxf86vm' 'sane' 'lcms' 'poppler-qt5' 'chmlib' 'djvulibre' 'libspectre' 'ebook-tools'
         'json-c' 'libkexiv2' 'libkdcraw')
makedepends=('pkg-config' 'extra-cmake-modules' 'kdoctools' 'qca-qt5')
conflicts=(kdegraphics-okular okular)
provides=(okular)
source=("git://anongit.kde.org/okular.git#branch=frameworks")
install=$pkgname.install
sha256sums=('SKIP')

pkgver() {
  cd okular
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../okular \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DKF5KExiv2_DIR=/usr/lib/cmake/KEXIV2
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
