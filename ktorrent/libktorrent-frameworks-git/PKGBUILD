# Maintainer: ahjolinna <ahjolinna@yahoo.com>

pkgname=libktorrent-frameworks-git
pkgver=1.9.50.r396.77d90c4
pkgrel=1
#ktorver=4.3.1
pkgdesc="A BitTorrent program for KDE"
arch=('x86_64')
url="http://ktorrent.pwsp.net/"
license=('GPL2')
depends=('karchive' 'kcrash' 'ki18n' 'kio' 'solid' 'qca-qt5')
makedepends=('extra-cmake-modules' 'boost' 'doxygen' 'kdoctools' 'git')
options=('libtool')
source=('git://anongit.kde.org/libktorrent#branch=master')
#source=('git://anongit.kde.org/libktorrent.git')

provides=('libktorrent')
conflicts=('libktorrent-frameworks')

sha1sums=('SKIP')

pkgver() {
  cd libktorrent
  _ver="$(cat CMakeLists.txt | grep -m1 LIBKTORRENT_VERSION | cut -d '"' -f2)"
  echo -e "${_ver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libktorrent \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
