# Maintainer: ahjolinna <ahjolinna@yahoo.com>

_pkgname="mesa-demos"
pkgname="glxinfo"
pkgver=8.2.0
pkgrel=1
arch=("i686" "x86_64")
pkgdesc="Display info about a GLX extension and OpenGL renderer. Standalone binary from the mesa-demos project."
url="http://www.mesa3d.org/"
license=("MIT")
depends=("libgl")
makedepends=("glew")
conflicts=("$_pkgname")
source=("ftp://ftp.freedesktop.org/pub/mesa/demos/$pkgver/$_pkgname-$pkgver.tar.bz2")
sha256sums=("e4bfecb5816ddd4b7b37c1bc876b63f1f7f06fda5879221a9774d0952f90ba92")


build() {
	mkdir -p build
	cd build
	"$srcdir/$_pkgname-$pkgver/configure" --prefix=/usr
	make -C src/xdemos bin_PROGRAMS="$pkgname"
}

package() {
	cd build
	make DESTDIR="$pkgdir" -C src/xdemos bin_PROGRAMS="$pkgname" install
}
 
