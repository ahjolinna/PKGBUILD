# Maintainer: ahjolinna <ahjolinna@yahoo.com>

pkgname=soundkonverter-frameworks-git
pkgver=v2.1.90.r25.gdf74e99
pkgrel=1
pkgdesc="Front-end to various audio converters"
arch=('x86_64')
url="http://www.kde-apps.org/content/show.php?content=29024"
license=('GPL')

depends=('ki18n' 'kxmlgui' 'kio' 'solid' 'kconfig' 'kconfigwidgets' 'kwidgetsaddons' 'kdelibs4support'
         'libkcddb-frameworks-git' 'taglib' 'ffmpeg' 'cdparanoia' 'phonon-qt5' 'sox' 'vorbis-tools' 'wavpack' 'speex'
         'flac' 'timidity++')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
optdepends=('cdrkit: cdda2wav backend'
            'faac: faac backend'
            'faad2: faad backend'
            'fluidsynth: flouidsynth backend'
            'lame: lame backend'
            'mac: mac backend'
            'mplayer: mplayer backend'
            'mp3gain: mp3gain backend'
            'opus-tools: opus backend'
            'twolame: twolame backend'
            'vorbisgain: vorbisgain backend')
source=("soundkonverter::git+https://github.com/valeth/soundkonverter#branch=frameworks")
provides=("soundkonverter" "soundkonverter-frameworks")
conflicts=("soundkonverter" "soundkonverter-frameworks")
sha512sums=('SKIP')
install=$pkgname.install

pkgver() {
  cd "$srcdir/soundkonverter"
  ( 
    set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's|\([^-]*-g\)|r\1|;s|-|.|g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
    cd "$srcdir"
    [[ -d build ]] && rm -rf build
    mkdir -p build
}

build() {
    cd "$srcdir/build"

    cmake "$srcdir/soundkonverter/src" \
        -DKF5_BUILD=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \

  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
}

