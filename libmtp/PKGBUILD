# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: damir <damir@archlinux.org>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=libmtp
pkgver=1.1.11
pkgrel=1
pkgdesc="Library implementation of the Media Transfer Protocol"
arch=("i686" "x86_64")
url="http://libmtp.sourceforge.net"
license=('LGPL')
depends=('libusb')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('eea14dd30ddd08bbe39cfcb57564a4b8')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-udev=/usr/lib/udev
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # fix broken udev rule
  sed -i "/^Unable to open/d" ${pkgdir}/usr/lib/udev/rules.d/69-libmtp.rules
}
