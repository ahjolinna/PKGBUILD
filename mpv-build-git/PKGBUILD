# Maintainer: ahjolinna <ahjolinna@yahoo.com>

pkgname=mpv-build-git
pkgver=20160118.a99b63d
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg). (GIT version)"
arch=('x86_64')
depends=('desktop-file-utils'
         'smbclient'
         'libguess'
         'enca'
         'libxv'
         'libfdk-aac'
         'libcdio-paranoia'
         'openal'
         #'lua52' not yet in repos
         'lua'
         'libssh'
         'libcaca'
         'rsound'
         'libxss'
         'libdvdnav'
         'jack2'
         'libbluray'
         'libpulse'
         'libaacplus'
         'x264'
         'x265'
         'libbs2b'
         'libgme'
         'mesa'
         'libxinerama'
         'libxrandr'
         'libxkbcommon'
         'hicolor-icon-theme'
         'sdl2'
         'lcms2'
         'lame'
         'libva'
         'rubberband'
         'uchardet'
         'libarchive'
         'libsoxr'
         'v4l-utils'
         'libvdpau'
         'libva-intel-driver'
         'vapoursynth'
            )
license=('GPL2' 'GPL3')
url="http://mpv.io"
screenshot='https://i.warosu.org/data/g/img/0474/54/1428688179637.png'
makedepends=('git'
             'python3-docutils'
             'yasm'
             'ladspa'
             'fontconfig'
             'fribidi'
             )
optdepends=('youtube-dl: Another way to view youtuve videos with mpv'
            'zsh-completions: Additional completion definitions for Zsh users'
            'livestreamer: to watch live video streams (twitch.tv)'
            'vapoursynth-plugin-mvtools: Realtime motion interpolating playback in mpv')
provides=('mpv')
conflicts=('mpv')
options=('!emptydirs')
install=mpv-build-git.install
source=('git+https://github.com/mpv-player/mpv-build.git'
        'git+https://github.com/mpv-player/mpv.git'
        'ffmpeg::git+https://github.com/FFmpeg/FFmpeg.git'
        'git+https://github.com/libass/libass.git'
        #'git+http://anongit.freedesktop.org/git/fribidi/fribidi.git'
        )
sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          'SKIP'
          #'SKIP'
          )
backup=('etc/mpv/encoding-profiles.conf')

pkgver() {
  cd mpv
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd mpv-build
  ln -s ../mpv
  ln -s ../ffmpeg
  ln -s ../libass
 #ln -s ../fribidi

  # Set ffmpeg/libass/fribidi/mpv flags
  echo "--disable-programs \
  --enable-libx264 \
  --enable-libx265 \
  --enable-libfreetype \
  --enable-libbluray  \
  --enable-libpulse \
  --enable-libopus  \
  --enable-libvorbis \
  --enable-libvpx \
  --enable-libxvid \
  --enable-libmp3lame \
  --enable-libfdk-aac \
  --enable-libtheora \
  --enable-libssh \
  --enable-nonfree \
  --enable-ladspa \
  --enable-libbs2b  \
  --enable-avisynth  \
  --enable-avresample  \
  --enable-fontconfig \
  --enable-libaacplus \
  --enable-libass \
  --enable-openal \
  --enable-opengl \
  --enable-runtime-cpudetect \
  --enable-swresample \
  --enable-vdpau" > ffmpeg_options
  
  echo "--prefix=/usr \
  --confdir=/etc/mpv \
  --enable-openal \
  --disable-sdl1 \
  --enable-sdl2 \
  --enable-libmpv-shared \
  --enable-zsh-comp \
  --enable-pulse \
  --enable-egl-x11 \
  --enable-wayland \
  --enable-vdpau \
  --enable-vaapi \
  --enable-libsmbclient \
  --enable-dvdread \
  --enable-libarchive \
  --enable-gpl3 \
  --enable-vapoursynth \
  --enable-vdpau-hwaccel  \
  --enable-vaapi-hwaccel" > mpv_options

 # Download Waf script
  #./bootstrap.py
   # point python to python3
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python3|' ${srcdir}/mpv/bootstrap.py
  # initialize
  cd mpv
  ./bootstrap.py
  sed -i 's/vendor-completions/site-functions/' wscript_build.py

  # waf looking for python2...
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python3|' ${srcdir}/mpv/waf
  sed -i 's/$? != 0/$? >> 8 != 0/' TOOLS/zsh.pl
}

build() {
  cd mpv-build
  ./build
}

package() {
  cd mpv-build
  DESTDIR="${pkgdir}" ./install

  install -Dm755 mpv/TOOLS/mpv_identify.sh "${pkgdir}/usr/bin/mpv-identify"
  install -Dm755 mpv/TOOLS/umpv "${pkgdir}/usr/bin/umpv"

  install -Dm644 mpv/DOCS/encoding.rst "${pkgdir}/usr/share/doc/mpv/encoding.rst"
  install -Dm644 mpv/DOCS/edl-mpv.rst "${pkgdir}/usr/share/doc/mpv/edl-mpv.rst"
  install -Dm644 mpv/DOCS/client-api-changes.rst "${pkgdir}/usr/share/doc/mpv/client-api-changes.rst"
  install -Dm644 mpv/DOCS/contribute.md "${pkgdir}/usr/share/doc/mpv/contribute.md"

  sed 's|/usr/local/etc/mpv.conf|/etc/mpv.conf|g' -i "${pkgdir}/usr/share/doc/mpv/mpv.conf"

  (cd mpv/TOOLS/lua; for i in $(find . -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/mpv/scripts/${i}"; done)

  (cd mpv/DOCS/client_api_examples; for i in $(find . -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/mpv/client_api_examples/${i}"; done)
}
