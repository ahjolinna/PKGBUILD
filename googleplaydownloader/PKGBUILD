# Maintainer: ahjolinna <ahjolinna@yahoo.com>
pkgname=googleplaydownloader
pkgver=1.8
pkgrel=1
pkgdesc="A graphical software to download APKs from the Google Play store."
arch=(any)
url="https://codingteam.net/project/googleplaydownloader"
screenshot='http://3.bp.blogspot.com/-hJLpYBEXVGE/VFN6-8AvJMI/AAAAAAAAUwg/C1R_wAZyJ_I/s1600/google-play-downloader.png'
license=('AGPL')
depends=('python2'
         'python2-protobuf'
         'python2-requests'
         'python2-ndg-httpsclient'
         'python2-pyasn1'
         'wxpython')
optdepends=('java-runtime: needed for autogeneration of a new AndroiID')
makedepends=('gendesk')
options=(!emptydirs)
source=("https://codingteam.net/project/googleplaydownloader/download/file/googleplaydownloader_${pkgver}.orig.tar.gz"
        "googleplaydownloader"
        "googleplaydownloader.png::https://codingteam.net/project/googleplaydownloader/upload/icon.png")
sha1sums=('41c913f74975c8298f042906901021edac1161b9'
          '154d3ade5d56fdfdc1f7d590f7e06bdf6aba0bfa'
          '61de3e6b29fc82bacf98c8de9e3ad4cec6c70631')
 
prepare() {
    cd "$srcdir"
    gendesk -f \
        --pkgname="$pkgname" \
        --pkgdesc="$pkgdesc"
}
 
package() {
    cd "$srcdir/$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir/" --optimize=1
 
    cd "$srcdir"
    install -D --mode=755 "googleplaydownloader" "$pkgdir/usr/bin/googleplaydownloader"
    install -D --mode=644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -D --mode=644 "$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}