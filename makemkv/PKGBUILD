# Maintainer: ahjolinna <ahjolinna@yahoo.com>
# Contributor: Francesco Marinucci <franzmari[at]chakra-project[dot]it>

pkgname=makemkv
pkgver=1.9.9
pkgrel=1
pkgdesc="DVD and Blu-ray to MKV converter and network streamer"
arch=('x86_64')
url="http://www.makemkv.com"
screenshot='http://www.makemkv.com/images/sshot-t3-web.png'
license=('LGPL' 'MPL' 'custom')
depends=('qt5-base' 'libxkbcommon' 'hicolor-icon-theme' 'icu' 'ffmpeg')
  optdepends=('lib32-glibc: dts support')
install=makemkv.install
source=(${url}/download/${pkgname}-bin-${pkgver}.tar.gz
        ${url}/download/${pkgname}-oss-${pkgver}.tar.gz
        makemkv.1
        makemkvcon.1
        mmdtsdec.1)
md5sums=('22ae44280ad88d87d9150743d167f4e5'
         '5bdfdb4fd5e5136787ecfa8856f485c8'
         '1f9b3a91427a2015434e501542443f4c'
         '7f4b112c5178860cc2eb25059ae1af2a'
         '9476154228bf1b1f983178ba8565ac44')

build() {
  cd "${srcdir}/${pkgname}-oss-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-oss-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/${pkgname}-bin-${pkgver}"
  install -d "${pkgdir}/usr/bin/"
  install -t "${pkgdir}/usr/bin/" bin/i386/mmdtsdec
    install -t "${pkgdir}/usr/bin/" bin/amd64/makemkvcon
  install -d "${pkgdir}/usr/share/MakeMKV"
  install -m 644 -t "${pkgdir}/usr/share/MakeMKV" src/share/makemkv_*.mo.gz src/share/*.mmcp.xml
  
  install -Dm 644 src/eula_en_linux.txt "${pkgdir}/usr/share/licenses/${pkgname}/eula_en_linux.txt"

  cd "${srcdir}/"
  install -d "${pkgdir}/usr/share/man/man1/"
  install -m 644 -t "${pkgdir}/usr/share/man/man1/" makemkv.1 makemkvcon.1 mmdtsdec.1
}
