#Maintainer: ahjolinna <ahjolinna@yahoo.com>

pkgname=yle-dl
pkgver=2.10.1
pkgrel=1
_gitid=c6b59f1
pkgdesc="Download video and audio from YLE Areena."
arch=("any")
url="http://aajanki.github.io/yle-dl/"
screenshot='http://apk-dl.com/detail/image/com.yle.webtv-w250.png'
license=("GPL2")
depends=('python2-crypto' 'php-mcrypt')
optdepends=('python2-youtube-dl: new lightweight backend'
            'rtmpdump: old rtmpdump backend')
conflicts=('rtmpdump-yle')
replaces=('rtmpdump-yle')
_adobehdsversion=2016.02.22

source=("yle-dl-${pkgver}.tar.gz::https://github.com/aajanki/yle-dl/tarball/${pkgver}"
        "AdobeHDS-${_adobehdsversion}.php::https://raw.githubusercontent.com/K-S-V/Scripts/fc77568a3ea1c52f1a6ca488bdedc6fd2dd82552/AdobeHDS.php")
        
sha256sums=('430db2b1ca92c973b3b98134eed498d718dce8505a21c92f38a03a8f109adb91'
            'f84cfca3c9ec7be5cc49ff44424b3a540622265e4d14d544d8c6582bc346c9ff')

         
prepare() {
  cd "${srcdir}/aajanki-${pkgname}-${_gitid}"

  sed 's|/usr/local|/usr|' -i yle-dl
  sed "s|'php'|'php', '-d extension=bcmath.so', '-d extension=curl.so', '-d extension=mcrypt.so'|" -i yle-dl
}

package() {
  cd "${srcdir}/aajanki-${pkgname}-${_gitid}"

  make prefix=/usr DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/AdobeHDS-${_adobehdsversion}.php" "${pkgdir}/usr/share/yle-dl/AdobeHDS.php"
}


