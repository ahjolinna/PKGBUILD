# Maintainer: ahjolinna: <ahjolinna@yahoo.com>

pkgname='pipelight'
pkgver=0.2.8
pkgrel=6
pkgdesc="A browser plugin which allows one to use windows only plugins inside Linux browser"
url="https://launchpad.net/pipelight"
screenshot='http://pipelight.net/cms/images/pipelight-logo.svg'
arch=('x86_64')
license=('GPL2' 'LGPL2.1' 'MPL')

depends=('wine-staging>=1.8' 'ttf-ms-fonts' 'wget' 'cabextract' 'unzip' 'gnupg' 'kde-baseapps-lib')
depends+=('lib32-libsm' 'lib32-libgl')

# Set value to 1 if you want to compile pluginloader yourself
_compilepluginloader=0

if [[ $_compilepluginloader != 0 ]]; then
  makedepends=('mingw-w64-gcc')
fi

makedepends+=('cabextract' 'xz')

#source=("https://bitbucket.org/mmueller2012/pipelight/get/v${pkgver%.*}-${pkgver##*.}.tar.bz2"
#  "https://launchpad.net/pipelight/trunk/0.1/+download/pluginloader-prebuilt-v${pkgver%.*}-${pkgver##*.}.tar.gz")
source=("https://bitbucket.org/mmueller2012/pipelight/get/v${pkgver}.tar.bz2"
  "pluginloader-v${pkgver}.tar.gz::http://repos.fds-team.de/pluginloader/v${pkgver}/pluginloader.tar.gz"
  "gnupg_fix.patch")
noextract=("pluginloader-v${pkgver}.tar.gz")

md5sums=('9fcbc7019a49eb0c2f613eaba0e96df6'
         '71b595924b8c8d91c830c2a897362ad2'
         'ac561ff3cc7df2bc3805567780ed545c')

install=pipelight.install

_srcdir=mmueller2012-pipelight-b7b5e5471d52/

#this changes the install location for the pipelight executables
_prefix=/usr

#change this if your wine-silverlight is installed elsewhere, or if you prefer a different wine directory
_wine=/usr

prepare() {
  pushd $_srcdir

  if [[ $_compilepluginloader == 0 ]]; then
    ln -sf ../pluginloader-v${pkgver}.tar.gz .
  fi

  patch -p1 < ../gnupg_fix.patch
  pushd share/

  #in case you want to use 64bit wine, you would need this
  #if [[ "$CARCH" == "x86_64" ]]; then
  #  sed -i 's|Files|Files (x86)|g' configs/pipelight*
  #  sed -i 's|Files|Files (x86)|g' install-dependency
  #fi

  popd
  popd
}

build() {
  pushd $_srcdir
  if [[ $_compilepluginloader == 0 ]]; then
    ./configure --prefix=$_prefix --wine-path=$_wine/bin/wine --gcc-runtime-dlls="" --win32-prebuilt
  else
    ./configure --prefix=$_prefix --wine-path=$_wine/bin/wine --gcc-runtime-dlls="" --win32-static
  fi
  make
  popd
}

package() {
  make -C $_srcdir PREFIX=$_prefix DESTDIR=$pkgdir install

  #if [[ $_compilepluginloader  == 0 ]]; then
  #  install -Dm644 src/windows/pluginloader.exe ${pkgdir}/$_prefix/share/pipelight/.
  #fi
# All plugin creation has now been moved to pipelight.install

# fix man page flags
  chmod 0644 ${pkgdir}/usr/share/man/man1/pipelight-plugin.1
}

# vim:set ts=2 sw=2 et:
