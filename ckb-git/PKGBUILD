# Maintainer: ahjolinna <ahjolinna@yahoo.com>
pkgname=ckb-git
pkgver=v0.2.2.r1.g5889c00
pkgrel=1
pkgdesc="Corsair Keyboard Input Driver (K65 RGB, K70[RGB], K95[RGB], M65 RGB)"
arch=('x86_64')
url="https://github.com/ccMSC/ckb"
screenshot='http://www.technobuffalo.com/wp-content/uploads/2014/10/CorsairGaming-K70RGB_05.jpg'
license=('GPL2')
depends=('qt5-base' 'systemd' 'zlib' 'gcc' 'glibc')
makedepends=('git')
replaces=('ckb-git-latest')
install=ckb-git.install
source=('ckb-git::git://github.com/ccMSC/ckb.git')
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}


build() {
  cd "$srcdir/${pkgname%-VCS}"
  qmake
  make QMAKE=/usr/lib/qt5/bin/qmake
}

package() {
  cd "$srcdir/${pkgname%-VCS}"

  install -dm755 "$pkgdir/opt/ckb/ckb-animations"
  install -Dm755 bin/ckb{,-daemon} "$pkgdir/opt/ckb/"
  install -D bin/ckb-animations/* "$pkgdir/opt/ckb/ckb-animations/"

  install -dm755 "$pkgdir/usr/bin"
  ln -s /opt/ckb/ckb "$pkgdir/usr/bin/"
  ln -s /opt/ckb/ckb-daemon "$pkgdir/usr/bin/"
  
  install -Dm644 usr/ckb.png "${pkgdir}/usr/share/icons/hicolor/512x512/apps/ckb.png"
  install -Dm644 usr/ckb.desktop "${pkgdir}/usr/share/applications/ckb.desktop"

  install -Dm644 service/systemd/ckb-daemon.service "${pkgdir}/usr/lib/systemd/system/ckb-daemon.service"
}
