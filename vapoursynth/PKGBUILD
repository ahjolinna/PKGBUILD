# $Id$
# Maintainer: ahjolinna <ahjolinna@yahoo.com>


pkgname=vapoursynth
pkgver=R31
pkgrel=1
pkgdesc='A video processing framework with the future in mind'
arch=('x86_64')
url='http://www.vapoursynth.com/'
license=('LGPL2.1' 'custom:OFL')
depends=('gcc-libs' 'glibc' 'imagemagick' 'libass' 'python3' 'shared-mime-info'
         'tesseract'
         'zimg')
makedepends=('python3-cython' 'python3-sphinx' 'yasm')
optdepends=('vapoursynth-plugin-mvtools: Realtime motion interpolating playback in mpv')
install='vapoursynth.install'
source=("vapoursynth-${pkgver}.tar.gz::https://github.com/vapoursynth/vapoursynth/archive/${pkgver}.tar.gz"
        'vapoursynth.xml')
sha256sums=('63e4e1eb8b51387bb7d82397604e7322e50b6368e07e34e2d85f7644ff62ec29'
            '8e51579547d20cd7cb9618a47b3ac508423d09d76649bf038d0ab9acb850b068')

build() {
  cd vapoursynth-${pkgver}

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --enable-imwri \
    --disable-static
  make
}

package() {
  cd vapoursynth-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/{licenses/vapoursynth,mime/packages}
  install -m 644 ofl.txt "${pkgdir}"/usr/share/licenses/vapoursynth/
  install -m 644 ../vapoursynth.xml "${pkgdir}"/usr/share/mime/packages/
}

# vim: ts=2 sw=2 et:

