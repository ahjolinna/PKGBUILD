# $Id$
# Maintainer: ahjolinna <ahjolinna@yahoo.com>

pkgname=x265-newer
pkgver=1.8
pkgrel=1
pkgdesc='Open Source H265/HEVC video encoder'
arch=('x86_64')
url='https://bitbucket.org/multicoreware/x265'
screenshot='http://cdn.afterdawn.com/v3/news/original/Hevc.jpg'
license=('GPL')
depends=('gcc-libs')
makedepends=('yasm' 'cmake')
provides=('x265')
replaces=('x265')
conflict=('x265')
source=("https://bitbucket.org/multicoreware/x265/downloads/x265_${pkgver}.tar.gz")
md5sums=('8b7ef9bc0b5bd26965d05a4508effeed')

prepare() {
  cd x265_11047

  for d in 8 $([[ $CARCH == 'x86_64' ]] && echo "10 12"); do
    if [[ -d build-$d ]]; then
      rm -rf build-$d
    fi
    mkdir build-$d
  done
}

build() {

    cd x265_11047/build-12

    cmake ../source \
      -DCMAKE_INSTALL_PREFIX='/usr' \
      -DHIGH_BIT_DEPTH='TRUE' \
      -DMAIN12='TRUE' \
      -DEXPORT_C_API='FALSE' \
      -DENABLE_CLI='FALSE' \
      -DENABLE_SHARED='FALSE'
    make

    cd ../build-10

    cmake ../source \
      -DCMAKE_INSTALL_PREFIX='/usr' \
      -DHIGH_BIT_DEPTH='TRUE' \
      -DEXPORT_C_API='FALSE' \
      -DENABLE_CLI='FALSE' \
      -DENABLE_SHARED='FALSE'
    make

    cd ../build-8

    ln -s ../build-10/libx265.a libx265_main10.a
    ln -s ../build-12/libx265.a libx265_main12.a

    cmake ../source \
      -DCMAKE_INSTALL_PREFIX='/usr' \
      -DENABLE_SHARED='TRUE' \
      -DEXTRA_LIB='x265_main10.a;x265_main12.a' \
      -DEXTRA_LINK_FLAGS='-L.' \
      -DLINKED_10BIT='TRUE' \
      -DLINKED_12BIT='TRUE'
    make

}

package() {
  cd x265_11047/build-8

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
